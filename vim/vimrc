" vim: sw=2 ts=2 sts=2 expandtab ft=vim
set nocompatible

"let g:james_minimal_plugins=1
let mapleader = ","
nnoremap \ ,
nnoremap <Leader><C-S> :source ~/.vimrc \| e<CR>


set sw=2 ts=2 sts=2
set background=dark
set cedit=                    " Key to open command-line window
set comments=sl:/*,mb:*,elx:*/  " auto format comment blocks
set complete=.,w,b,u            " No tags...nor included files, whatever they are...
set expandtab                   " Tabs are spaces, not tabs
set filetype=on
set foldenable                  " Auto fold code
set foldtext=MyFoldText()
set grepprg=rg\ --vimgrep\ --no-heading\ --smart-case
set hidden                      " Allow buffer switching without saving
set ignorecase                  " Case insensitive search
set lazyredraw
set linespace=0                 " No extra spaces between rows
set list
set matchpairs+=<:>             " Match, to be used with %
set matchtime=2
set mouse=
set mousehide                   " Hide the mouse cursor while typing
set nobackup                    " Not sure about this one
set nocursorline
set nohlsearch
set nojoinspaces                " Prevents inserting two spaces after punctuation on a join (J)
set nospell
set noswapfile                  " Not sure about this one
set nowrap
set omnifunc=syntaxcomplete#Complete
set scrolljump=-9               " Lines to scroll when cursor leaves screen
set shortmess+=filmnrxoOtT      " Abbrev. of messages (avoids 'hit enter')
set showmatch                   " Show matching brackets/parenthesis
set showmode                    " Display the current mode
set smartcase                   " Case sensitive when uc present
set splitbelow                  " Puts new split windows to the bottom of the current
set splitright                  " Puts new vsplit windows to the right of the current
set switchbuf=useopen
set syn=on
set t_Co=256
set textwidth=0
set title
set viewoptions=folds,options,cursor,unix,slash " Better Unix / Windows compatibility
set virtualedit=onemore         " Allow for cursor beyond last character
set whichwrap=b,s,h,l,<,>,[,]   " Backspace and cursor keys wrap too
set wildignore+=*/.git/*,*/tmp/*,*.so,*.swp,*.zip
set wildmode=list:longest,full  " Command <Tab> completion, list matches, then longest common part, then all.
set winminheight=0              " Windows can be 0 lines high
set dictionary+=/usr/share/dict/words
set iminsert=1

" Use local bundles if available
source ~/.vimrc.bundles

" Color stuff!
call James_Zenburn()

" Enforce alt consistency between vim and nvim; nvim-only sets
if has('nvim')
  set inccommand=nosplit
else
  " for cha in ['a', 'b', 'c', 'd', 'e', 'f', 'g', 'h', 'i', 'j', 'k', 'l', 'm', 'n', 'o',
  " \ 'p', 'q', 'r', 's', 't', 'u', 'v', 'w', 'x', 'y', 'z', 'A', 'B', 'C', 'D', 'E', 'F',
  " \ 'G', 'H', 'I', 'J', 'K', 'L', 'M', 'N', 'O', 'P', 'Q', 'R', 'S', 'T', 'U', 'V', 'W',
  " \ 'X', 'Y', 'Z', '0', '1', '2', '3', '4', '5', '6', '7', '8', '9', '0']
    " execute 'nmap <Esc>'.cha.' <A-'.cha.'>'
    " execute 'cmap <Esc>'.cha.' <A-'.cha.'>'
    " execute 'nmap <Esc><C-'.cha.'> <C-A-'.cha.'>'
    " execute 'cmap <Esc><C-'.cha.'> <C-A-'.cha.'>'
  " endfor
endif

" Override vim-sensible
runtime! plugin/sensible.vim
set listchars=tab:›\ ,trail:•,extends:❱,precedes:❰,nbsp:○
set notimeout nottimeout
set nrformats=
set scrolloff=3

" Tags stuff
let gitroot = substitute(system('git rev-parse --show-toplevel'), '[\n\r]', '', 'g')
if findfile(".tags", ".;") =~ "tags"
  set tags=./.tags;/
elseif gitroot != ''
    let &tags = &tags . ',' . gitroot . '/.git/.tags'
    let &tags = &tags . ',' . gitroot . '/.git/tags'
    let &tags = &tags . ',' . gitroot . '/.tags'
    let &tags = &tags . ',' . gitroot . '/tags'
endif

if has('persistent_undo')
  set undofile
  set undodir=~/.vimundo//
  set undolevels=1000
  set undoreload=10000
endif

" Stupid shift key fixes
command! -bang -nargs=* -complete=file E e<bang> <args>
command! -bang -nargs=* -complete=file W w<bang> <args>
command! -bang -nargs=* -complete=file Wq wq<bang> <args>
command! -bang -nargs=* -complete=file WQ wq<bang> <args>
command! -bang Wa wa<bang>
command! -bang WA wa<bang>
command! -bang Q q<bang>
command! -bang QA qa<bang>
command! -bang Qa qa<bang>

" Change Working Directory to that of the current file
nnoremap <silent> <Leader>wd :lcd %:p:h<CR>

" Visual stuff!
"     Visual shifting does not exit Visual mode
xnoremap < <gv
xnoremap > >gv
"     Allow using the repeat operator with a visual selection: http://stackoverflow.com/a/8064607/127816
xnoremap . :normal .<CR>
nnoremap vv 0v$h
nnoremap VV _v$h
nnoremap vV v$h
nnoremap Vv v_

" Incremement character under cursor only
nnoremap <Leader>ic a <C-C>h<C-A>lxh

" http://vim.wikia.com/wiki/Restore_cursor_to_file_position_in_previous_editing_session
function! ResCur()
  if line("'\"") <= line("$")
    normal! g`"
    return 1
  endif
endfunction
augroup resCur
  au!
  au! BufWinEnter * call ResCur()
augroup END

" Yank and Pasting stuff
nmap Y y$
cnoremap <C-T> <C-R>
nnoremap <Leader>p <Nop>
nnoremap <Leader>pl mm$p`m
nnoremap <Leader>pL mmA<Space><C-C>p`m
nnoremap <Leader>ph mm0P`m
nnoremap <Leader>pH mmI<Space><C-C>P`m
nnoremap <Leader>pj mmo<C-C>p`m
nnoremap <Leader>pJ mm3o<C-C>kp`m
nnoremap <Leader>pk mmO<C-C>p`m
nnoremap <Leader>pK mm3O<C-C>kp`m
nnoremap <Leader>pp mm)O<CR><C-R>"<C-C>`m
nnoremap <Leader>pP mm(O<C-R>"<CR><C-C>`m

if v:version > 702
  " Keep window position
  au! BufLeave * let b:winview = winsaveview()
  au! BufEnter * if(exists('b:winview')) | call winrestview(b:winview) | endif
  " JSON
  nmap <Leader>jp :%!underscore print<CR>:set filetype=json<CR><F5>
  nmap <Leader>jt :%!python -mjson.tool<CR>:set filetype=json<CR><F5>
  xmap <Leader>jp :!underscore print<CR>:set filetype=json<CR><F5>
  xmap <Leader>jt :!python -mjson.tool<CR>:set filetype=json<CR><F5>
else
  " JSON
  nmap <Leader>jt :%!python2.6 -mjson.tool<CR>:set filetype=json<CR><F5>
  xmap <Leader>jt :!python -mjson.tool<CR>:set filetype=json<CR><F5>
  if !has('nvim')
    " Backspace whatever. hack...
    inoremap  <BS>
  endif
endif
if v:version >= 704
  " NumberToggle
  function! NumberToggle()
    if(&rnu == 1 && &nu == 1)
      set nonumber norelativenumber
    else
      set number relativenumber
    endif
  endfunction
  if !exists('g:james_minimal_plugins')
    set number relativenumber
  endif
else
  " NumberToggle
  function! NumberToggle()
    if(&rnu == 1 && &nu == 1)
      set nonumber
    else
      set number
    endif
  endfunction
  set number
endif

function! NfAlphaToggle()
  if(&nrformats == "")
    set nrformats=alpha
  else
    set nrformats=
  endif
endfunction

function! HeightToggle()
  if(&winheight == 999)
    set winheight=1
  else
    set winheight=999
  endif
endfunction

function! ShcfToggle()
  if(&shcf == "-c")
    set shcf=-ci
  else
    set shcf=-c
  endif
endfunction

function! DiffW()
  let opt = ""
  if &diffopt =~ "icase"
    let opt = opt . "-i "
  endif
  if &diffopt =~ "iwhite"
    let opt = opt . "-w " " vim uses -b by default
  endif
  "if a:1 != ""
    "let opt = opt . "-G " . a:1
  "endif
  silent execute "!diff -a --binary " . opt .
        \ v:fname_in . " " . v:fname_new .  " > " . v:fname_out
endfunction

function! DiffWToggle()
  if &diffopt =~ "iwhite"
    set diffopt-=iwhite
    set diffexpr=
  else
    set diffopt+=iwhite
    set diffexpr=DiffW()
  endif
  echom &diffopt &diffexpr
endfunction

function! DiffWToggleRegex()
  set diffopt+=iwhite
  set diffexpr=DiffW()
  echom &diffopt &diffexpr
endfunction

function! ShowBreakToggle()
  if(&showbreak == g:james_showbreak)
    set showbreak=
  else
    let &showbreak=g:james_showbreak
  endif
endfunction

function! LineBreakToggle()
  if(&linebreak == 1)
    set nolinebreak
  else
    set linebreak
  endif
endfunction

function! ClipboardToggle()
  if(&clipboard == 'unnamedplus')
    set clipboard=
  else
    set clipboard+=unnamedplus
  endif
endfunction

" Center on search term, diff
function! MiddleToggle()
  if(maparg('n', 'n') == '')
    nmap n nzz
    nmap N Nzz
    nnoremap <A-n> n
    nnoremap <A-N> N
    nmap ]c ]czz
    nmap [c [czz
    nnoremap <A-c> ]c
    nnoremap <A-C> [c
  else
    nunmap n
    nunmap N
    nunmap <A-n>
    nunmap <A-N>
    nunmap ]c
    nunmap [c
    nunmap <A-c>
    nunmap <A-C>
  endif
endfunction

function! LeximaToggle()
  if(g:lexima_no_default_rules == 1)
    let g:lexima_no_default_rules=0
    call lexima#set_default_rules()
    let b:lexima_disabled=0
  else
    if(b:lexima_disabled == 1)
      let b:lexima_disabled=0
    else
      let b:lexima_disabled=1
    endif
  endif
endfunction

function! TabSpaces()
  let l:char=nr2char(getchar())
  if(l:char == 'r')
    execute ":retab"
  else
    let l:num=l:char+0
    setl expandtab
    let &l:sw=l:num
    let &l:ts=l:num
    let &l:sts=l:num
  endif
endfunction

function! RegisterFromUnnamed()
  let l:char=nr2char(getchar())
  execute ":let @" . l:char . "=@\""
endfunction

function! UnnamedFromRegister()
  let l:char=nr2char(getchar())
  execute ":let @\"=@" . l:char
endfunction

" Sets and toggles
xnoremap <Leader>s <Nop>
nnoremap <Leader>s <Nop>
nnoremap <Leader>sn :call NumberToggle()<CR>
nnoremap <Leader>sf :call NfAlphaToggle()<CR>
nnoremap <Leader>sh :call HeightToggle()<CR>
nnoremap <Leader>sS :call ShcfToggle()<CR>
nnoremap <Leader>sd :call DiffWToggle()<CR>
nmap     <Leader>sD :call DiffWToggleRegex("")<C-B><C-B>
nnoremap <Leader>sb :call ShowBreakToggle()<CR>
nnoremap <Leader>sB :call LineBreakToggle()<CR>
nnoremap <Leader>sC :call ClipboardToggle()<CR>
nnoremap <Leader>sm :call MiddleToggle()<CR>
nnoremap <Leader>sa :call LeximaToggle()<CR>
nnoremap <Leader>sN :set nu!<CR>
"nnoremap <Leader>svt :call VirtualToggle()<CR>
nnoremap <Leader>sv <Plug>VLToggle
nnoremap <Leader>sc :setl spell!<CR>
nnoremap <Leader>sp :setl paste!<CR>
nnoremap <Leader>sl :setl cursorline! \| set cursorcolumn!<CR>
nnoremap <expr> <Leader>st ":call TabSpaces()<CR>"
nnoremap <Leader>sw :setl wrap!<CR>
nnoremap <Leader>sW :windo setl wrap!<CR>
nnoremap <Leader>sL :if &linebreak \| setl nowrap nolinebreak \| else \| setl wrap linebreak \| endif<CR>
nnoremap <silent> <Leader>/ :setl invhlsearch<CR>
nnoremap <Leader>sz :setl foldmethod=manual<CR>

" Break-lines stuff
let g:james_showbreak="↳↳"
let &showbreak=g:james_showbreak
if v:version > 703 && has('patch338')
  set breakindent
  set breakindentopt=min:0,shift:-2, sbr:↳↳
endif

" Add/subtract stuff
function! AddSubtract(char, back)
  let pattern = &nrformats =~ 'alpha' ? '[[:alpha:][:digit:]]' : '[[:digit:]]'
  call search(pattern, 'cw' . a:back)
  execute 'normal! ' . v:count1 . a:char
  silent! call repeat#set(":\<C-U>call AddSubtract('" .a:char. "', '" .a:back. "')\<CR>")
endfunction
nnoremap <silent> <C-A> :<C-U>call AddSubtract("\<C-A>", '')<CR>
nnoremap <silent> <A-a>   :<C-U>call AddSubtract("\<C-A>", 'b')<CR>
nnoremap <silent> <C-X> :<C-U>call AddSubtract("\<C-X>", '')<CR>
nnoremap <silent> <A-x>   :<C-U>call AddSubtract("\<C-X>", 'b')<CR>

function! ETW(what, ...)
  for f1 in a:000
    let files = glob(f1)
    if files == ''
      execute a:what . ' ' . escape(f1, '\ "')
    else
      for f2 in split(files, "\n")
        execute a:what . ' ' . escape(f2, '\ "')
      endfor
    endif
  endfor
endfunction

" Edit multiple
command! -complete=file -nargs=+ Etabs call ETW('tabnew', <f-args>)
command! -complete=file -nargs=+ Ewindows call ETW('edit', <f-args>)
command! -complete=file -nargs=+ Eswindows call ETW('new', <f-args>)
command! -complete=file -nargs=+ Evwindows call ETW('vnew', <f-args>)

" Some helpers to edit mode
cnoremap %% <C-R>=expand('%:h').'/'<CR>
nnoremap <Leader>ew :Ewindows<Space>
nnoremap <Leader>es :Eswindows<Space>
nnoremap <Leader>ev :Evwindows<Space>
nnoremap <Leader>et :Etabs<Space>
nmap <Leader>eW :Ewindows %%
nmap <Leader>eS :Eswindows %%
nmap <Leader>eV :Evwindows %%
nmap <Leader>eT :Etabs %%
nnoremap <Leader>EW :enew<CR>
nnoremap <Leader>ES :new<CR>
nnoremap <Leader>EV :vnew<CR>
nnoremap <Leader>ET :tabnew<CR>
nnoremap <Leader>Ew :b<Space>
nnoremap <Leader>Es :sb<Space>
nnoremap <Leader>Ev :vert sb<Space>
nnoremap <Leader>Et :tab sb<Space>

function! NextTWorB()
  if (tabpagenr("$") == 1) && (winnr("$") == 1)
    bn
  elseif (tabpagenr("$") == 1)
    wincmd w
  else
    tabn
  endif
endfunction
function! PrevTWorB()
  if (tabpagenr("$") == 1) && (winnr("$") == 1)
    bp
  elseif (tabpagenr("$") == 1)
    wincmd W
  else
    tabp
  endif
endfunction
function! NumTorB(number)
  if (tabpagenr("$") == 1)
    execute ":b" . a:number
  else
    execute ":tabn" . a:number
  endif
endfunction

" Buffer stuff
nnoremap <silent> <Leader>b  <Nop>
nnoremap <silent> <Leader>bl :buffers<CR>
nnoremap <silent> <Leader>bt :tab ball<CR>
nnoremap <silent> <Leader>bd :bd<CR>
nnoremap <silent> <Leader>bD :bd!<CR>
nnoremap <silent> <Leader>Bd :call DeleteHiddenBuffers()<CR>
nnoremap <silent> <Leader>BD :call ReallyDeleteHiddenBuffers()<CR>
" Window stuff
nnoremap <silent> <C-H>      <C-W>h
nnoremap <silent> <C-J>      <C-W>j
nnoremap <silent> <C-K>      <C-W>k
nnoremap <silent> <C-L>      <C-W>l
nnoremap <silent> <C-A-J> :call SmoothDown()<CR>
nnoremap <silent> <C-A-K> :call SmoothUp()<CR>
" Tab stuff
nnoremap <silent> <A-i>     :bp<CR>
nnoremap <silent> <A-o>     :bn<CR>
nnoremap <silent> <A-h>     :tabmove -1<CR>
nnoremap <silent> <A-l>     :tabmove +1<CR>
" Smart Tab, (Window), Buffer stuff (and remappings for J, K)
nnoremap <silent> J     :call PrevTWorB()<CR>
nnoremap <silent> K     :call NextTWorB()<CR>
nnoremap <silent> <A-j>     J
nnoremap <silent> <A-k>     K
" Numbered buffers and tabs
for n in range(1, 9)
  execute 'nnoremap <A-'.n.'> :call NumTorB('.n.')<CR>'
endfor
nnoremap <A-0> :call NumTorB(10)<CR>
" <C-C> doesn't trigger InputLeave
inoremap <C-C> <Esc>

function! SmoothDown()
  if(&winheight == 999)
    wincmd j
  else
    set winheight=999
    wincmd j
    set winheight=1
  endif
endfunction

function! SmoothUp()
  if(&winheight == 999)
    wincmd k
  else
    set winheight=999
    wincmd k
    set winheight=1
  endif
endfunction

function! DeleteHiddenBuffers()
  let tpbl=[]
  call map(range(1, tabpagenr('$')), 'extend(tpbl, tabpagebuflist(v:val))')
  for buf in filter(range(1, bufnr('$')), 'bufexists(v:val) && index(tpbl, v:val)==-1')
    silent execute 'bwipeout' buf
  endfor
endfunction

function! ReallyDeleteHiddenBuffers()
  let tpbl=[]
  call map(range(1, tabpagenr('$')), 'extend(tpbl, tabpagebuflist(v:val))')
  for buf in filter(range(1, bufnr('$')), 'bufexists(v:val) && index(tpbl, v:val)==-1')
    silent execute 'bwipeout!' buf
  endfor
endfunction

" Register stuff!
" {{{
"TODO: name register for all below
nnoremap <Leader>c <Nop>
nnoremap <Leader>x <Nop>
nnoremap <Leader>r <Nop>
xnoremap <Leader>c <Nop>
"Line to xclip
nnoremap <silent> <Leader>C _y$:silent call system("xclip -i -selection clipboard", getreg("\""))<CR>
xnoremap <silent> <Leader>C y:silent call system("xclip -i -selection clipboard", getreg("\""))<CR>
"Paste from xclip
nnoremap <silent> <Leader>v :silent call setreg("\"",system("xclip -o -selection clipboard"))<CR>p
xnoremap <silent> <Leader>v :silent call setreg("\"",system("xclip -o -selection clipboard"))<CR>p
nnoremap <silent> <Leader>V :silent call setreg("\"",system("xclip -o -selection clipboard"))<CR>P
xnoremap <silent> <Leader>V :silent call setreg("\"",system("xclip -o -selection clipboard"))<CR>P

"Special swapping mappings!
"Register from xclip
nnoremap <silent> <Leader>D :silent call setreg("\"",system("xclip -o -selection clipboard"))<CR>
"xclip from register
nnoremap <silent> <Leader>F :silent call system("xclip -i -selection clipboard", getreg("\""))<CR>
"Swap registers
nnoremap <expr> <Leader>R ":call RegisterFromUnnamed()<CR>"
nnoremap <expr> <Leader>U ":call UnnamedFromRegister()<CR>"

"Register from full path
nnoremap <silent> <Leader>rp :silent call setreg("\"", expand("%:p"))<CR>
"Xclip from full path
nnoremap <silent> <Leader>xp :silent call system("xclip -i -selection clipboard", expand("%:p"))<CR>
"Register from filename
nnoremap <silent> <Leader>rf :silent call setreg("\"", expand("%:t"))<CR>
"Xclip from filename
nnoremap <silent> <Leader>xf :silent call system("xclip -i -selection clipboard", expand("%:t"))<CR>
"Register from dir
nnoremap <silent> <Leader>rd :silent call setreg("\"", expand("%:p:h")."/")<CR>
"xclip from dir
nnoremap <silent> <Leader>xd :silent call system("xclip -i -selection clipboard", expand("%:p:h")."/")<CR>
"Register from Git repo
nnoremap <silent> <Leader>rg :silent call setreg("\"", system("git rev-parse --show-toplevel"))<CR>
"xclip from Git repo
nnoremap <silent> <Leader>xg :silent call system("git rev-parse --show-toplevel \| tr -d '\n' \| xclip -i -selection clipboard")<CR>
"Register from buffers
nnoremap <silent> <Leader>rb :silent call setreg( "\"", join(map(filter(range(0,bufnr('$')), 'buflisted(v:val)'), 'fnamemodify(bufname(v:val), ":p")'), " ") )<CR>
"xclip from buffers
nnoremap <silent> <Leader>xb
      \ :silent call system(
        \ "xclip -i -selection clipboard",
        \ join(map(filter(range(0,bufnr('$')), 'buflisted(v:val)'), 'fnamemodify(bufname(v:val), ":p")'), " ")
      \ )<CR>
"Export buffers
nnoremap <silent> <Leader>be
      \ :silent call writefile(
        \ map(filter(range(0,bufnr('$')), 'buflisted(v:val)'), 'fnamemodify(bufname(v:val), ":p")'),
        \ system("echo /home/james/.buffersave/`date +\"\%Y\%m\%d_\%H:\%M:\%S\"`")
      \ )<CR>
"Register from All
nmap <silent> <Leader>ra yae<C-O>
"xclip from All
nmap <silent> <Leader>xa yae<C-O>:silent call system("xclip -i -selection clipboard", getreg("\""))<CR>
" }}}

" Saving stuff
cmap w!! w !sudo tee % >/dev/null
nmap ,ww! :w<CR>:bw<CR>
cmap ww!<CR> w<CR>:bw<CR>
" Wut
nmap ,w!w!w! :w!!<CR>ll:bw<CR>
cmap w!w!w! :w!!<CR>ll:bw<CR>

" Header stuff
"Header make
nnoremap <Leader>Hm ^i\|\|<Space><C-C>A<Space>\|\|<C-C>"qyy"qP"qD999a=<C-C>jhlk"qD"qyyj"qpqqqk^
"Header fix
nnoremap <Leader>Hf k999A=<C-C>jhlk"qD"qyyj"qpj"qddkkkqqqj^
"Header unmake
nnoremap <Leader>Hu k"qdd"qx"qx"qx$"qX"qX"qxj"qddqqqk^
"Minor header make
nnoremap <Leader>hm I\|__<Space><C-C>A<Space>__\|<C-C>yyPElv$Bhr<Space>^r<Space>$r<Space>x
"Minor header fix
nnoremap <Leader>hf "yyykV"yp^r<Space>$xBhv^Elr<Space>
"Minor header unmake
nnoremap <Leader>hu ^4x$x3Xkdd
"Markdown header make
nnoremap <Leader>MHm yypVr=k0
"Markdown header fix
nnoremap <Leader>MHf yyjVpVr=k0
"Markdown header unmake
nnoremap <Leader>MHu j"_ddk0
"Markdown minor header make
nnoremap <Leader>Mhm yypVr-k0
"Markdown minor header fix
nnoremap <Leader>Mhf yyjVpVr-k0
"Markdown minor header unmake
nnoremap <Leader>Mhu j"_ddk0

" Bundles folding
"Create fold
nnoremap <Leader>BF yyo" }}}<C-C>k>>PI" <C-C>A {{{<C-C>j_
"Remove fold TODO

" Remove whitespace
nnoremap <silent> <F5> mm:let _s=@/<Bar>%s/\s\+$//e<Bar>let @/=_s<Bar>nohl<CR>`m
xnoremap <silent> <F5> mm:<Home>let _s=@/<Bar><End>s/\s\+$//e<Bar>let @/=_s<Bar>nohl<CR>`m

" Emacs stuff!
inoremap <C-F> <Right>
cnoremap <C-F> <Right>
inoremap <C-B> <Left>
cnoremap <C-B> <Left>
inoremap <C-D> <Delete>
cnoremap <C-D> <Delete>
cnoremap <C-A> <Home>
cnoremap <A-b> <S-Left>
cnoremap <A-f> <S-Right>

" Dedent input mode (since <C-D> is taken above; <C-T> is indent)
inoremap <C-G> <C-D>

" No accidental Ex-mode or keyword-lookup
nnoremap gQ gggqG
nnoremap <Leader>Q Q

" Escape from edit-and-execute-command
nnoremap QQ ggVGd:wq

" Execute viml
nnoremap <Leader>e. :execute getline(".")<CR>
nnoremap <Leader>er : <C-R>"<CR>

" Check coloring
map <F8> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<' . synIDattr(synID(line("."),col("."),0),"name") . "> lo<" . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">" . " FG:" . synIDattr(synIDtrans(synID(line("."),col("."),1)),"fg#")<CR>
map <F9> :echo "hi<" . synIDattr(synID(line("."),col(".")+1,1),"name") . '> trans<' . synIDattr(synID(line("."),col(".")+1,0),"name") . "> lo<" . synIDattr(synIDtrans(synID(line("."),col(".")+1,1)),"name") . ">" . " FG:" . synIDattr(synIDtrans(synID(line("."),col(".")+1,1)),"fg#")<CR>

" Expand/collapse braces
nmap <Leader>{ 2yEysi{<C-J>/<C-T>"<CR>
nmap <Leader>} 2yE?{<CR>Jh%kJ?<C-T>"<CR>
nmap <Leader>( 2yEysi(<C-J>/<C-T>"<CR>
nmap <Leader>) 2yE?(<CR>J"_xh%kJ?<C-T>"<CR>
nmap <Leader>[ 2yEysi[<C-J>/<C-T>"<CR>
nmap <Leader>] 2yE?[<CR>J"_xh%kJx?<C-T>"<CR>

" swap j/gj, k/gk, $/g$, 0/g0
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k
xnoremap j gj
xnoremap k gk
xnoremap gj j
xnoremap gk k
" noremap $ g$
" noremap 0 g0
" noremap g$ $
" noremap g0 0
" TODO: make $/0 remaps contextual?

function! CombineSelection(line1, line2, cp)
  execute 'let char = "\u'.a:cp.'"'
  execute a:line1.','.a:line2.'s/\%V[^[:cntrl:]]/&'.char.'/ge'
endfunction

" Modify selected text using combining diacritics
command! -range -nargs=0 Overline        call CombineSelection(<line1>, <line2>, '0305')
command! -range -nargs=0 Underline       call CombineSelection(<line1>, <line2>, '0332')
command! -range -nargs=0 DoubleUnderline call CombineSelection(<line1>, <line2>, '0333')
command! -range -nargs=0 Strikethrough   call CombineSelection(<line1>, <line2>, '0336')
"xnoremap <Leader>z :Overline<CR>

" Merge stuff!
" Search for merge conflics
nnoremap <Leader>fc /^[<=>]\{7\}[^<=>]*$<CR>
" Obtain merge diffs
nnoremap dOR :diffg REMOTE<CR>
nnoremap dOB :diffg BASE<CR>
nnoremap dOL :diffg LOCAL<CR>
nnoremap dU :diffupdate<CR>

" Diff stuff!
function! DiffWithSaved()
  let filetype=&ft
  diffthis
  vnew | r # | normal! 1Gdd
  diffthis
  exe "setlocal bt=nofile bh=wipe nobl noswf ro ft=" . filetype
endfunction

nnoremap <Leader>dt :windo diffo \| windo difft<CR>
nnoremap <Leader>do :windo diffo<CR>
nnoremap <Leader>ds :call DiffWithSaved()<CR>

" Scalding stuff
nmap <Leader>HC :%s/ *\.incrementCounter(\([^")]*"[^"]*"[^")]*\)*)\n\?//ge<CR><F5>

" Python stuff
autocmd FileType python nnoremap <buffer> <Leader>cf :setlocal foldmethod=expr foldexpr=getline(v:lnum)=~'^\s*#'<CR>
autocmd FileType python nnoremap <buffer> <Leader>cF :set foldmethod=expr foldexpr=getline(v:lnum)=~'^\s*#'<CR>

" Last buffer more easily
nmap <BS> <C-^>

" Search for visual selection:
xnoremap <silent> <Leader>/ "9y:silent let @/ = '\V'.substitute(substitute(substitute(@9, '\\', '\\\\', 'ge'),'/', '\\/', 'ge'),'\n', '\\n', 'ge')<CR>
" Search for WORD under cursor (cWORD):
nnoremap <silent> <Leader>? :silent let @/ = '\V'.substitute(substitute(expand('<cWORD>'), '\\', '\\\\', 'ge'), '/', '\\/', 'ge')<CR>
" Test string with special characters:
" <Leader>/y'/<C-G>Vp\A\/<ESC>\V:"'<End>s!\\!\\\"\!g<CR>\V:<Home>silent<Space><E"nd>s!/''!\\/!g<:'"CR>\I\V<ESC>$hhhD<CR>

" No mouse scrolling
map <Up> <F18>
map <S-Up> <F18>
map <C-Up> <F18>
map <Down> <F18>
map <S-Down> <F18>
map <C-Down> <F18>
imap <Up> <Nop>
imap <S-Up> <Nop>
imap <C-Up> <Nop>
imap <Down> <Nop>
imap <S-Down> <Nop>
imap <C-Down> <Nop>
cmap <Up> <Nop>
cmap <S-Up> <Nop>
cmap <C-Up> <Nop>
cmap <Down> <Nop>
cmap <S-Down> <Nop>
cmap <C-Down> <Nop>

" Undo big insert-mode deletes
" Messes up C-U after new line...TODO: fix?
" inoremap <C-W> <C-C>a<C-W>
" inoremap <C-U> <C-C>a<C-U>

" Fast horizontal navigation
nnoremap <A-H> zH
nnoremap <A-L> zL
nnoremap <A-J> 25zh
nnoremap <A-K> 25zl
" Or maybe:
"nnoremap <C-B> zH
"nnoremap <C-F> zL
"nnoremap <A-J> 15jzz
"nnoremap <A-K> 15kzz
"nnoremap <A-H> 25zh
"nnoremap <A-L> 25zl

" Move screen around, like zb/zt/zz
nnoremap zB zb9<C-E>
nnoremap zT zt9<C-Y>

" Fold stuff
function! MyFoldText()
  let line = getline(v:foldstart)
  let sub = substitute(line, '/\*\|\*/\|{{{\d\=', '', 'g')
  let poorDevil = substitute(sub, '^\( *\)" ', '\1', '')
  let pad = repeat(' ', (winwidth(0)/2)-strlen(line)-strlen(v:folddashes))
  return poorDevil . v:folddashes . pad
endfunction
if !exists('g:james_pager')
  nmap <C-\> za
else
  nmap <A-\> za
endif

" Set syntax
nmap <Leader>tc :set ft=csv syn=csv \| call plug#load('csv.vim')<CR>
nmap <Leader>ts :set ft=scala syn=scala<CR>
nmap <Leader>td :set ft=drake syn=drake<CR>
nmap <Leader>t<Space> :set ft= syn= formatprg= linebreak showbreak= wrap<CR>
nmap <Leader>t<CR> :set ft= syn= formatprg= nolinebreak showbreak=g:james_showbreak nowrap<CR>

" TODO: Smaller buffer numbers: instead of creating a new buffer sometimes, open the lowest-numbered empty hidden buffer.
" TODO: Function to line up word under cursor with <count>th instance of same word on line above or below.
" TODO: <Leader>L<Something> for :arglocal uses
" TODO: Remove <Leader>s options that duplicate unimpaired functionality? I do prefer mine...
" TODO: Fix position of space when pasting -- conditional on register matching pattern, toggle off?

" TODO: Just a reminder to namespace mappings...
"autocmd FileType unite call s:unite_keymaps()
"function! s:unite_keymaps()
   "" Play nice with supertab
  "let b:SuperTabDisabled=1
  "" Enable navigation with control-j and control-k in insert mode
  "imap <buffer> <C-j>   <Plug>(unite_select_next_line)
  "imap <buffer> <C-k>   <Plug>(unite_select_previous_line)
"endfunction`

" Convert markdown to pdf
" nnoremap <Leader>mm :!pandoc --standalone -f markdown -t latex -o %.pdf %; nohup evince %.pdf > /dev/null &<CR>
" nnoremap <Leader>mc :!pandoc --standalone -c style.css -f markdown -t latex -o %.pdf %; nohup evince %.pdf > /dev/null &<CR>
" nnoremap <Leader>mh :!pandoc --standalone -c style.css -f markdown -t html -o %.html %<CR>
autocmd! FileType markdown nnoremap <silent> <buffer> <Leader>m :silent w <Bar>
  \ silent !pandoc --standalone -c style.css -f markdown -t html -o %:r.html % && chromium-browser --headless --no-margins --print-to-pdf=%:r.pdf %:r.html <CR>
" TODO: Cleanup.

" Write to null (buffer doesn't look unsaved)
nnoremap <Leader>wn :w! /dev/null<CR>

" Redraw mapping
nnoremap <Leader><C-R> :redraw<CR>

" Completion stuff
" autocmd InsertLeave,CompleteDone * if pumvisible() == 0 | pclose | endif
inoremap <expr> <Tab>   pumvisible() ? "\<C-N>" : "\<Tab>"
inoremap <expr> <S-Tab> pumvisible() ? "\<C-P>" : "\<S-Tab>"

" Say IPA
map <F10> :!say <cWORD><CR>

" Digraphs
" forward tick: ´
digraphs `` 180
" bullet: •
digraphs ** 8226
" Double-strike 𝔼ℝℕℤℚℂⅈⅉⅇ
digraphs EE 120124
digraphs RR 8477
digraphs NN 8469
digraphs ZZ 8484
digraphs QQ 8474
digraphs CC 8450
digraphs ii 8520
digraphs jj 8521
digraphs ee 8519
" diagonal ellipses: ⋰ ⋱
digraphs ./ 8944
digraphs .\ 8945
" extended matrix brackets: ⎡⎢⎣⎤⎥⎦
digraphs bt 9121
digraphs bm 9122
digraphs bb 9123
digraphs bT 9124
digraphs bM 9125
digraphs bB 9126
" mathematical angle brackets: ⟨⟩
digraphs B< 10216
digraphs B> 10217
" angle brackets: 〈〉
digraphs b< 12296
digraphs b> 12297
" evaluate:
"   left parenthesis: ⎛⎜⎝
"   right parenthesis: ⎞⎟⎠
"   left curly bracket: ⎧⎨⎩
"   curly bracket extension: ⎪
"   right curly bracket: ⎫⎬⎭
"   integral extension: ⎮
"   curly bracket sections: ⎰⎱
"   large summation: ⎲⎳
"   vertical square brackets: ⎴⎵⎶
"   radical symbol bottom: ⎷
"   vertical box lines: ⎸⎹
"   horizontal lines: ⎺⎻⎼⎽⎯
" Easy greek letters
inoremap <expr> <C-S> "".nr2char(getchar())."*"
cnoremap <expr> <C-S> "".nr2char(getchar())."*"
lnoremap <expr> <C-S> "".nr2char(getchar())."*"
" Matrix brackets
nnoremap <silent> <C-S>[ :silent let @" = "⎡⎤\n⎢⎥\n⎣⎦\n"<CR>
" Combining Diacritics
inoremap <C-S>^ <C-V>u0302
cnoremap <C-S>^ <C-V>u0302
lnoremap <C-S>^ <C-V>u0302
inoremap <C-S>~ <C-V>u0303
cnoremap <C-S>~ <C-V>u0303
lnoremap <C-S>~ <C-V>u0303
inoremap <C-S>_ <C-V>u0332
cnoremap <C-S>_ <C-V>u0332
lnoremap <C-S>_ <C-V>u0332

" TODO: Make digraphs. Extract to file?
" Super upper:
" ᴬᴮᴰᴱᴳᴴᴵᴶᴷᴸᴹᴺᴼᴾᴿᵀᵁⱽᵂ
" Super small-caps (probably don't bother):
" ᶦᶫᶰᶸ
" Super lower:
" ᵃᵇᶜᵈᵉᶠᵍʰⁱʲᵏˡᵐⁿᵒᵖʳˢᵗᵘᵛʷˣʸᶻ
" Sub lower:
" ₐₑₕᵢⱼₖₗₘₙₒₚᵣₛₜᵤᵥₓ
" Super lower greek:
" ᵝᵞᵟᵋᶿᶥᵠᵡ
" Sub lower greek:
" ᵦᵧᵨᵩᵪ
" Super combining lower aeioucdhmrtvx:
" U+0363 U+0364 U+0365 U+0366 U+0367 U+0368 U+0369 U+036A U+036B U+036C U+036D U+036E U+036F
" Extended brackets: https://unicode-search.net/unicode-namesearch.pl?term=BRACKET

" Straighten notes - refreshes syntax, so gets weird if defined in syntax file
function! Straighten()
  bufdo 1
  source $VIMRUNTIME/syntax/syntax.vim
endfunction
nnoremap <Leader>n mM:call Straighten()<CR>`M

" Newline with no comments
nnoremap <silent> <C-S> o<Esc>0D
